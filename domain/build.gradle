apply plugin: 'java'
apply plugin: ibidem.gradle.python.PythonPlugin
apply plugin: 'org.jruyi.thrift'
apply plugin: 'maven'

def generatedSourcesRoot = "$buildDir/generated-sources"
def generatedJavaSources = "$generatedSourcesRoot/gen-javabean"
def generatedPythonSources = "$generatedSourcesRoot/gen-py"

logger.quiet("Building $parent - $project - $version")

compileThrift {
    sourceDir 'thrift'
    outputDir generatedSourcesRoot
    thriftExecutable 'thrift-0.9.1'

    generator 'java', 'beans,hashcode'
    generator 'py', 'new_style,utf8strings,slots'
}

compileJava.dependsOn compileThrift

buildPython {
    dependsOn compileThrift
    inputs.source generatedPythonSources

    collector { details, target ->
        if (details.name != "__init__.py") {
            details.copyTo(target)
        } else if (details.relativePath.segments.length == 1) {
            // Skip
        } else if (details.relativePath.segments.length <= 3) {
            target.text = "__import__('pkg_resources').declare_namespace(__name__)\n"
        } else {
            details.copyTo(target)
        }
    }
}

jar {
    baseName 'codetanks-domain'
}

sourceSets {
    main {
        java {
            srcDirs generatedJavaSources
        }
    }
}

dependencies {
    compile 'org.apache.thrift:libthrift:0.9.1'

    python (
        ':thrift:0.9.1',
        ':nose',
        ':wheel'
    )
}

buildscript {
    dependencies {
        classpath 'org.jruyi.gradle:thrift-gradle-plugin:0.2.0'
    }
}
